plugins {
    //Android Gradle Plugin
    alias(libs.plugins.android.application)
    //Kotlin
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    //KSP (Kotlin Symbol Processing)
    alias(libs.plugins.com.google.devtools.ksp)
    //Dependency Injection
    alias(libs.plugins.dagger.hilt.android)
    //Navigation
    alias(libs.plugins.androidx.navigation.safeargs)
}

hilt {
    enableAggregatingTask = true
}

android {
    namespace appId
    compileSdk appCompileSdkVersion

    defaultConfig {
        applicationId appId
        minSdk appMinSdkVersion
        targetSdk appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        buildFeatures.buildConfig true
        buildConfigField('boolean', 'DEBUG', '' + DEBUG + '')
        buildConfigField('String', 'HOST_URL', '"' + HOST_URL + '"')
    }

    buildTypes {
        debug {
            // thêm hậu tố vào tên gói
            applicationIdSuffix ".debug"
            // chế độ gỡ lỗi
            debuggable true
            jniDebuggable true
            // Loại bỏ các tài nguyên vô dụng
            shrinkResources false
            // xáo trộn mã
            minifyEnabled false
            // set app name
            addManifestPlaceholders(["app_name": "@string/app_name" + " Debug"])
        }

        release {
            // chế độ gỡ lỗi
            debuggable false
            jniDebuggable false
            // Loại bỏ các tài nguyên vô dụng
            shrinkResources true
            // xáo trộn mã
            minifyEnabled true
            // set app name
            addManifestPlaceholders(["app_name": "@string/app_name"])
            // set proguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    //Material Design
    implementation(libs.material)

    //AppCompat
    implementation(libs.androidx.appcompat)

    //Activity
    implementation(libs.androidx.activity)

    //Layout
    implementation(libs.androidx.constraintlayout)

    //Navigation
    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.navigation.ui)

    //Testing
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)

    //Android Core
    implementation(libs.androidx.core)

    //Kotlin
    implementation(libs.kotlin.stdlib)

    //Dependency Injection
    implementation(libs.hilt.android)
    kapt(libs.hilt.compiler)

    //Database
    implementation(libs.room)
    implementation(libs.room.runtime)
    ksp(libs.room.compiler)

    //Networking
    implementation(libs.retrofit)
    implementation(libs.retrofit.converter.gson)
    implementation(libs.retrofit.converter.moshi)

    //Moshi
    implementation(libs.moshi)
    implementation(libs.moshi.ktx)
    implementation(libs.moshi.adapter)

    //OkHttp
    implementation(libs.okhttp)
    implementation(libs.okhttp.logging.interceptor)

    //JSON Processing
    implementation(libs.gson)

    //Image Loading
    implementation(libs.glide)
    ksp(libs.glide.ksp)

    //Multidex
    implementation(libs.androidx.multidex)

    //Coroutines
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.kotlinx.coroutines.core)

    //Lifecycle
    implementation(libs.lifecycle.runtime)
    implementation(libs.lifecycle.viewmodel)

    //Preferences
    implementation(libs.preference.ktx)

    //Log
    implementation(libs.timber)

    //Crypto
    implementation(libs.androidx.security.crypto.ktx)

    //Chucker
    debugImplementation(libs.chucker.interceptor)
    releaseImplementation(libs.chucker.interceptor.no.op)
}
